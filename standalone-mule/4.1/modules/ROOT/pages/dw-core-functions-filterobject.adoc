= filterObject
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[filterobject1]]
== filterObject&#40;{ &#40;K&#41;?: V }, &#40;value: V, key: K, index: Number&#41; &#45;&#62; Boolean&#41;: { &#40;K&#41;?: V }

Filters an object, keeping the key-value pairs that fulfill the `criteria`.


The criteria (a lambda expression) has three parameters: `value`, `key`, and
`index`. You can reference the value with `&#36;`, the key with `&#36;&#36;`,
and the index with `&#36;&#36;&#36;`.

=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| `value` | The input object.
| `criteria` | The expression that determines whether to retain the key-value pair or not.
|===

=== Example

This example keeps only the key-value pairs if the value equals `"a"`.

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
output application/json
---
{"letter1": "a", "letter2": "b"} filterObject ((value) -> value == "a")
----

==== Output

[source,JSON,linenums]
----
{
  "letter1": "a"
}
----

=== Example

This example only keeps the key-value pairs where the key starts with "letter".

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
{"letter1": "a", "letter2": "b", "id": 1} filterObject ((value, key) -> key startsWith "letter")
----

=== Example

This example outputs an object that contains only the values that are not `null` in the input JSON object.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
var o = {
    str1 : "String 1",
    str2 : "String 2",
    str3 : null,
    str4 : "String 4",
}
---
o filterObject $ != null
----

==== Output

[source,json,linenums]
----
{
  "str1": "String 1",
  "str2": "String 2",
  "str4": "String 4"
}
----


[[filterobject2]]
== filterObject&#40;Null, &#40;value: Nothing, key: Nothing, index: Nothing&#41; &#45;&#62; Boolean&#41;: Null

Helper function that allows `filterObject` to work with null values.
